;;; -*- mode: common-lisp; coding: utf-8; package: tsdb -*-

(in-package :tsdb)

;;;
;;; [incr tsdb()] --- Competence and Performance Profiling Environment
;;; Copyright (c) 2008 -- 2009 Stephan Oepen (oe@ifi.uio.no)
;;;
;;; This program is free software; you can redistribute it and/or modify it
;;; under the terms of the GNU Lesser General Public License as published by
;;; the Free Software Foundation; either version 2.1 of the License, or (at
;;; your option) any later version.
;;;
;;; This program is distributed in the hope that it will be useful, but WITHOUT
;;; ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or
;;; FITNESS FOR A PARTICULAR PURPOSE.  See the GNU Lesser General Public
;;; License for more details.
;;; 

(defparameter *conll-type* :starsem)

(defun read-items-from-conll-file (file
                                   &key (base 1) (offset 0) shift
                                        (type *conll-type*))
  (when (probe-file file)
    (with-open-file (stream file :direction :input)
      (loop
          with id = base
          with input = nil
          for line = (read-line stream nil nil)
          while line
          when (string= line "")
          collect
            (let* ((i id)
                   (length (length input))
                   (string (format nil "狺撖" (nreverse input)))
                   (tokens (conll-preprocess string :type type :format :raw))
                   (negations
                    (when (eq type :starsem)
                      (handler-case (starsem-summarize-tokens tokens)
                        (condition (condition)
                          (warn (format nil "a" condition))
                          nil)))))
              (if (eq type :starsem)
                (labels ((segment (chapter)
                           (cond
                            ;;
                            ;; based on chapter names, carve up the identifier
                            ;; space as follows, one segment per chapter:
                            ;; [0-1] wisteria
                            ;; [2-17] baskervilles
                            ;;
                            ((search "wisteria" chapter :end2 8)
                             (- (parse-integer chapter :start 8) 1))
                            ((search "baskervilles" chapter :end2 12)
                             (+ (parse-integer chapter :start 12) 1))
                            ((search "cardboard" chapter :end2 9)
                             16)
                            ((search "circle" chapter :end2 6)
                             (+ (parse-integer chapter :start 6) 16)))))
                  (let* ((token (first tokens))
                         (segment (or (segment (get-field :chapter token)) 0))
                         (sentence (get-field :sentence token)))
                    (setf i (+ 10000 (* segment 1000) sentence))))
                (when (functionp shift) (setf i (funcall shift i))))
              (incf id)
              (when (consp tokens)
                (setf tokens (write-to-string tokens :case :downcase)))
              (when (consp negations)
                (setf negations (write-to-string negations :case :downcase)))
              (setf input nil)
              (pairlis '(:i-id :i-wf :i-length :i-input :i-tokens :i-comment)
                       (list (+ offset i) 1 length string tokens negations)))
          else do (push line input)))))

(defun conll-preprocess (string &key (format :string) gaps (mode :erg) type)
  (let ((tokens
         (loop
             for token in (ppcre:split "\\n" string)
             for conll = (ppcre:split "\\t" token)
             when (eq type :starsem)
             collect
               ;;
               ;; the 2012 *SEM pseudo-CoNLL format
               ;;
               (loop
                   for key in '(:chapter :sentence :id :form :stem :ppos :ptb)
                   for value = (pop conll)
                   when (smember key '(:sentence :id))
                   collect (cons key (parse-integer value :junk-allowed t))
                   into token
                   else collect (cons key value) into token
                   finally
                     (let* ((id (get-field :id token))
                            (length (length (get-field :form token))))
                       (set-field :start (* id 100) token)
                       (set-field :end (+ (* id 100) length) token))
                     (return (acons :starsem conll token)))
             else when (= (length conll) 10)
             collect
               ;;
               ;; the earlier, CoNLL 2007 format
               ;;
               (loop
                   for key in '(:id :form :stem
                                :cpos :pos :feat
                                :head :deprel :phead :pdeprel)
                   for value in conll
                   when (smember key '(:id :head :phead))
                   collect (cons key (parse-integer value :junk-allowed t))
                   else collect (cons key value))
             else collect
               ;;
               ;; the richer, CoNLL 2009 format
               ;;
               (loop
                   for key in '(:id :form :stem :plemma
                                :pos :ppos :feat :pfeat
                                :head :phead :deprel :pdeprel
                                :fillpred :pred :apreds)
                   for value in conll
                   when (smember key '(:id :head :phead))
                   collect (cons key (parse-integer value :junk-allowed t))
                   else collect (cons key value)))))
    (case format
      (:raw tokens)
      (:string
       (let ((forms
              (loop
                  for token in tokens
                  unless (and (null gaps) (eq mode :srg)
                              (string= (get-field :stem token) "_")
                              (string= (get-field :pos token) "p"))
                  collect (get-field :form token))))
         (format nil "狺" forms))))))

(defun conll-for-pet (string &optional tagger
                      &key gold (characterize t) (mode :erg)
                           type stream)
  (declare (ignore tagger))
  (loop
      with result
      with i = 0
      with start = 0 
      with end = 1
      for token in (conll-preprocess string :format :raw :type type)
      for id = (get-field :id token)
      for form = (rewrite-conll-token (get-field :form token))
      for plemma = (get-field :plemma token)
      for lemma = (if (or gold (not plemma)) (get-field :stem token) plemma)
      for cpos = (get-field :cpos token)
      for pos 
      = (or cpos (if gold (get-field :pos token) (get-field :ppos token)))
      for feat = (if (or gold (not plemma))
                   (get-field :feat token)
                   (get-field :pfeat token))
      for yy 
      = (case mode
          (:jacy
           (let* ((feat (substitute #\+ #\| feat))
                  (feat (substitute #\- #\= feat)))
             (unless #+:lkb (lkb::punctuationp form) #-:lkb nil
               (format 
                nil 
                "(d, d, d, :[**;<a:a>, ]                  1, s, 0, \"null\"@[, \"a+a\" 1.0])"
                id i (+ i 1) characterize start end form pos feat))))
          (:srg
           (unless (and (string= lemma "_") (string= pos "p"))
             (loop
                 for tag in (conll-to-parole pos feat)
                 for stem 
                 = (if (ppcre:scan "^(?:[zwf]|np|ao)" tag)
                     (cond
                      ((and (char= (char tag 0) #\z)
                            (member form '("un" "una" "uno") :test #'string=))
                       form)
                      (t tag))
                     lemma)
                 for yy
                 = (format 
                    nil 
                    "(d, d, d, :[**;<a:a>, ]                     1, s \"a\", 0, \"$a\"@[, s 1.0])"
                    id i (+ i 1) characterize start end stem form tag pos)
                 when tag collect yy into result
                 finally
                   (when (and (null result) (not (string= pos "f")))
                     (format
                      t
                      "&conll-for-pet():                        ignoring token #a (`a' `a' `a')%"
                      id form pos feat))
                   (return result))))
          (t
           (let ((start (or (get-field :start token) i))
                 (end (or (get-field :end token) (+ i 1))))
             (format 
              nil 
              "(d, d, d, :[**;<a:a>, ]               1, s, 0, \"null\"@[, s 1.0])"
              id i (+ i 1) characterize start end form pos))))
      when (consp yy) do (setf result (nconc yy result)) (incf i)
      when (stringp yy) do (push yy result) (incf i)
      when yy do (setf start end) (setf end (+ start 1))
      else do (incf end)
      finally
        (setf result (nreverse result))
        (cond
         ((or (streamp stream) (eq stream t))
          (format stream "狺" (nreverse result)))
         ((stringp stream)
          (with-open-file (stream stream
                           :direction :output :if-exists :supersede)
            (format stream "狺" result))))
        (return (values (format nil "狺" result) i))))

(defun rewrite-conll-token (token &optional pos)
  (let ((token (rewrite-ptb-token token pos)))
    ;;
    ;; it would seem that, the CoNLL 2007 data at least, has braces instead of
    ;; parentheses.  i dimly recall reading something about this problem in an
    ;; early README file (maybe for the second PTB release), but i wonder why
    ;; CoNLL in 2007 should have this problem?                  (3-nov-10; oe)
    ;;
    (cond
     ((string-equal pos "{") "(")
     ((string-equal pos "}") ")")
     (t token))))

(defparameter *conll-parole-a-map*
  '((("postype=ordinal" . "o") ("postype=qualificative" . "q") 0)
    (0)
    (("gen=f" . "f") ("gen=m" . "m") ("gen=c" . "c") ("gen=c" . "0") 0)
    (("num=s" . "s") ("num=p" . "p") ("num=c" . "0") ("num=c" . "n") 0)
    (("posfunction=participle" . "p") 0)))

(defparameter *conll-parole-c-map*
  '((("postype=coordinating" . "c") ("postype=subordinating" . "s") 0)))

(defparameter *conll-parole-d-map*
  '((("postype=article" . "a") ("postype=demonstrative" . "d")
     ("postype=exclamative" . "e") ("postype=indefinite" . "i")
     ("postype=numeral" . "n") ("postype=possessive" . "p")
     ("postype=interrogative" . "t") 0)
    (("person=1" . "1") ("person=2" . "2") ("person=3" . "3") 0)
    (("gen=f" . "f") ("gen=m" . "m") ("gen=c" . "c") ("gen=c" . "n")
     ("gen=c" . "0") 0)
    (("num=s" . "s") ("num=p" . "p") ("num=c" . "0") ("num=c" . "n") 0)
    (("possessornum=s" . "s") ("possessornum=c" . "c")
     ("possessornum=p" . "p") 0)))
  
(defparameter *conll-parole-f-map*
  '((("punct=exclamationmark" . "a") ("punct=colon" . "d")
     ("punct=quotation" . "e") ("punct=hyphen" . "g") ("punct=slash" . "h")
     ("punct=etc" . "s") ("punct=semicolon" . "x") ("punct=mathsign" . "z") 0)
    (("punctenclose=open" . "a") ("punctenclose=close" . "t")
     ("punctenclose=close" . "c") 0)))

(defparameter *conll-parole-n-map*
  '((("postype=common" . "c") 0)
    (("gen=f" . "f") ("gen=m" . "m") ("gen=c" . "c") ("gen=c" . "0") 0)
    (("num=s" . "s") ("num=p" . "p") ("num=c" . "n") ("num=c" . "0") 0)
    (0)
    (0)
    (0)))

(defparameter *conll-parole-p-map*
  '((("postype=personal" . "p") ("postype=demonstrative" . "d")
     ("postype=possessive" . "x") ("postype=indefinite" . "i")
     ("postype=interrogative" . "t") ("postype=relative" . "r")
     ("postype=numeral" . "n") ("postype=exclamative" . "e") 0)
    (("person=1" . "1") ("person=2" . "2") ("person=3" . "3") 0)
    (("gen=f" . "f") ("gen=m" . "m") ("gen=c" . "0") ("gen=c" . "c")
     ("gen=c" . "n") 0)
    (("num=s" . "s") ("num=p" . "p") ("num=c" . "0") ("num=c" . "n") 0)
    (("case=nominative" . "n") ("case=accusative" . "a") ("case=dative" . "d")
     ("case=oblique" . "o") 0)
    (("possessornum=s" . "s") ("possessornum=p" . "p") ("possessornum=c" . "c")
     0)
    (("polite=yes" . "p") 0)))

(defparameter *conll-parole-v-map*
  '((("postype=auxiliary" . "a") ("postype=main" . "m")
     ("postype=semiauxiliary" . "s") 0)
    (("mood=indicative" . "i") ("mood=subjunctive" . "s")
     ("mood=imperative" . "m") ("mood=infinitive" . "n") ("mood=gerund" . "g")
     ("mood=pastparticiple" . "p") 0)
    (("tense=present" . "p") ("tense=imperfect" . "i") ("tense=future" . "f")
     ("tense=past" . "s") ("tense=conditional" . "c") 0)
    (("person=1" . "1") ("person=2" . "2") ("person=3" . "3") 0)
    (("num=s" . "s") ("num=p" . "p") ("num=c" . "0") 0)
    (("gen=f" . "f") ("gen=m" . "m") ("gen=c" . "0") 0)))

 
(defparameter *conll-parole-z-map*
  '((("postype=currency" . "m") ("postype=percentage" . "p") 0)))

(defun conll-to-parole (pos features &key (filter t))
  (labels ((cross-product (lists)
             (if (null (rest lists))
               (loop
                   for foo in (first lists) collect (list foo))
               (loop
                   with rests = (cross-product (rest lists))
                   for foo in (first lists)
                   nconc (loop
                             for bar in rests
                             collect (cons foo bar)))))
           (fields (map)
             (let ((fields
                    (loop
                        for field in map
                        collect
                          (loop
                              for entry in field
                              when (and (consp entry)
                                        (search
                                         (first entry)
                                         features :test #'string=))
                              collect (rest entry) into matches
                              finally (return (or matches (last field)))))))
               (loop
                   for values in (cross-product fields)
                   for tag = (format nil "a狺痫鲠祯弩麒孱矧铛祆骈祠弪＋红脞戾è簌礅镬ㄩ铘弪篝蜷铉躔汜箦翎绌红脞┅ㄧ弭栳箬簌礅镬祀夂邯戾殂犰蝓戾螵┅－红脞舂泔祆邈翎绌┅ㄣ镱è篝蜷铉痫⑨ㄦ殄熹泔铎飙疳蝻戾岘磲皙┅è篝蜷铉痫恽ㄦ殄熹泔铎飙疳蝻戾悱磲皙┅è篝蜷铉痫洧ㄣ镱è箦狎汨痫篝疱筋蹴弪犰驽狒躜弩扉篝Ⅹ┅ㄦ殄熹泔铎飙疳蝻戾洵磲皙┅┅è篝蜷铉痫㈡ㄣ镱è篝蜷铉驽狒躜弩瘐钽艚疱蜷镤扉篝㈡稷┅è篝蜷铉驽狒躜弩瘐钽艚怛徙脲酎瘐钽翦钽祜箦斤疱睥扉篝㈡疳┅è篝蜷铉驽狒躜弩瘐钽艚怛徙脲酎瘐钽翦钽祜箦姐祜箦扉篝㈡痿┅è篝蜷铉驽狒躜弩瘐钽艚聃弩糸镱磲螂瘐钽翦钽祜箦斤疱睥扉篝㈡獒┅è篝蜷铉驽狒躜弩瘐钽艚聃弩糸镱磲螂瘐钽翦钽祜箦姐祜箦扉篝㈡轸┅è篝蜷铉驽狒躜弩瘐钽艚泔眄幄扉篝㈡恽┅è篝蜷铉驽狒躜弩瘐钽艚怛徙脲酎瘐钽翦钽祜箦斤疱睥扉篝㈡汜┅è篝蜷铉驽狒躜弩瘐钽艚怛徙脲酎瘐钽翦钽祜箦姐祜箦扉篝㈡泗┅ㄦ殄熹泔铎飙疳蝻戾姝磲皙┅┅è篝蜷铉痫㈤扉篝㈤┅è篝蜷铉痫㈩ㄣ镱è篝蜷铉驽狒躜弩痫篝疱金蝻疱螯珏罱泓铛斫恽扉篝㈩鸢鞍鞍┅ㄦ殄熹泔铎飙疳蝻戾瞽磲皙┅┅è篝蜷铉痫稷ㄦ殄熹泔铎飙疳蝻戾瓠磲皙┅è篝蜷铉痫Ⅱㄣ镱è篝蜷铉驽狒躜弩痫篝疱筋彗狒轹澧扉篝Ⅱ睥┅è篝蜷铉驽狒躜弩⑦扉篝Ⅱ纰┅┅è篝蜷铉痫Ⅲㄣ镱è篝蜷铉驽狒躜弩痫篝疱金蝈痫箝糸镱珏罱睃铛斫簏泔铘蜥泗邃靳弩扉篝Ⅲ疸眢┅＋侯蹯è篝蜷铉驽狒躜弩痫篝疱金蝈痫箝糸镱珏罱睃铛斫瘘泔铘蜥泗邃靳弩扉篝Ⅲ疸眇┅è篝蜷铉驽狒躜弩痫篝疱金蝈痫箝糸镱珏罱泓铛斫恽扉篝Ⅲ痼鞍┅┅è篝蜷铉痫Ⅵㄦ殄熹泔铎飙疳蝻戾霏磲皙┅è篝蜷铉痫Ⅶ扉篝Ⅶ┅è篝蜷铉痫Ⅹㄦ殄熹泔铎飙疳蝻戾磲皙┅┅ㄤ彐躅轸屙麸泔铎ㄩ翦脲篝蝈犴舂顼熹蝈篚祠殇麒孱篝蜷铉篝蝈犴蝈趱蝾骝镯轸屙麸泔铎鏖翳镳孱骈戾篝蝈犴篝蝈犴轰轵邈糸镱猴豸瘐洪姝屮轶趔后躔弪箦溴ㄩ翦憝麸泔铎轸屙后趄遽篝蝈犴虹镬顼熹候弩蹯舡殇蝈篚祠殇┅┅麒孱篝蜷铉顼熹蝈趱蝾骝镯轸屙麸泔铎鏖翳镳孱骈戾ㄧ镬顼熹轰轵邈糸镱猴豸瘐洪姝屮轶趔后躔弪箦溴ㄩ翦憝麸泔铎轸屙后趄遽篝蝈犴虹镬顼熹候弩蹯舡殇蝈篚祠殇┅┅灬忮祗è桢徜铒溴镳糸镱犰桢徜螬ㄩ铛祆铒溴溽蹒梏弪铒溴┅戾舄è桢徜ㄦ轵篝桢徜螬┅扉篝ㄣ镱铒溴ㄩㄥ桢徜铒溴箦泔钿桢徜螬桢徜┅┅祜镳鏖翳桢徜铒溴桢徜铒溴鏖翳桢徜ㄩㄥ桢徜ㄦ轵篝桢徜螬桢徜ㄣ镱桢徜桢徜螬骘铒溴轭铒溴溽蹒梏弪铒溴狃疱钿ㄨ遽潴铒溴桢徜螬┅┅戾舄è轭瘐ㄧ弭骈屐洪轭瘐轸屙┅ㄣ镱祆矧ㄧ弭骈屐恒镱祆轸屙戾è泔铎ㄣ镱祆痱屦蝻沐篌轭瘐烘矧磲候狩┅钽镱轸屙ㄡ泔铙恒镱祆泔铎铋飑泔铎飑┅蝈篚祠ㄧ弭骈屐候弩蹯趔轸屙┅蝈篚祠麒孱铛礅弪蝈篚祠殇祜镳骘蝈篚祠轭蝈篚祠麒孱ㄥ耢ㄧ弭骈屐候弩蹯舡殇蝈篚祠蝈篚祠殇蝈趱蝾蝈篚祠┅蝈篚祠矧蝈篚祠ㄦ轵篝蝈篚祠螬┅ㄤ弪轹狒轱戾è溴蜷鲠糸镱ㄧ弭骈屐轰弪轹狒轱蝈篚祠┅í疳汶徵濯ㄦ轭洵疳汶徵呼箐猢┅麒孱篝蜷铉溴蜷鲠糸镱箦翩溴蜷鲠糸镱蝈徜骝镯篝蜷铉溴蜷鲠糸镱┅箦翩ㄧ弭骈屐轰弪轹狒轱蝈篚祠溴蜷鲠糸镱┅溴蜷鲠糸镱┅铒溴戾è溴蜷鲠糸镱蟓殓铒蝈麸脲铙皙铋飑ㄤ弪轹狒轱瞽麸铒溴溴蜷鲠糸镱┅ㄨ遽潴ㄨ遽潴铒溴┅麸翎癌ㄣ矧蝈泗癌灬忮祗è铛沆艴铒溴箦翩铒溴铛沆艴铒溴ㄩǐ铒溴骝镯铒溴暴铒溴麸铒溴┅铒溴麸铒溴祜镳鏖翳瘐钽趱狒轱Ж＼＼＼＼＼＼＼＼＼＼＼＼＼＼＼憨骘骝镯铒溴骝镯铒溴麸ō铒溴麸铒溴暴骘麸脲铘泔铎飑骘骘蝽ㄧ弭骈屐烘矧麸脲瞟躅戾篌祜镳骘徙蝻篌骘蝽犰麽礤礅弪瘐钽趱狒轱呼弩＇汨狎僵蝈趱蝾ǐ暴骈钺祆蝈趱蝾铒溴麸铒溴┅┅┅祜镳骘ㄤ屦孱溴铘桢徜轭桢徜躅戾篌铒溴铛沆艴溴疱钿孱舂滹铛沆艴溴疱钿孱舂躅戾篌矧铛祆桢徜铒溴铛沆艴桢徜┅滹铛沆艴桢徜┅祜镳骘麸脲轭泔铎骘殇ㄧ弭骈屐洪麸脲瞟骘铒溴戾è铒溴ㄦ轵篝ㄦ轵篝桢徜螬┅ㄩㄡ钿铒溴殇铒溴骝镯铒溴┅冀殇铒溴麸铒溴┅痫桢徜螬铒溴螬骘桢徜ㄩ蝈篝铒溴螬铒溴铛沆艴蝈篝铒溴螬癌滹换换翳筱矧弪轸箦屙蟋躞弩翳扰聊泔祯眍骘遽簌妁邂犰扉铉换镦蝈篚祠蟋瘐翳矧殓轭犰扰聊轭麸翳腥帕泔祯眍换麒孱篝蝈犴ㄦ矧磲篝蝈犴狺狺唼ア殇祜镳骘脲轭Ж烘矧后翦吼戾眄吼矬吼痫烘遽吼驽狒泔祆邈ㄧ弭骈屐脲麸脲瞟桢徜ㄧ弭骈屐鸿遽麸脲瞟ㄧ弭骈屐烘殪祓蝈麸脲瞟┅麒孱篝蝈犴顼熹ㄦ矧磲顼熹狺狺狺狺ア殇祜镳骘脲轭Ж烘矧后翦吼戾眄吼矬吼痫烘遽吼驽狒泔祆邈ㄧ弭骈屐脲麸脲瞟ㄧ弭骈屐鸿遽麸脲瞟ㄧ弭骈屐吼桢徜麸脲瞟祜镳骘脲轭Ж轰屦蝈吼溴痱屐烘殪祓蝈吼蝈横痱邃螬泔祆邈ㄧ弭骈屐脲麸脲瞟┅ㄩ钽麸翎飑麒孱ㄥ聃犰桢徜ㄧ弭骈屐鸿遽麸脲瞟ㄩ钽泔蝌邈舂┅麒孱篝蝈犴翦蝠蜷篝蝈犴┅麒孱篝蝈犴顼熹翦蝠蜷顼熹┅疳轵扉Ж呼雉犰鸿遽洎扉篝麸翎泔蝌邈舂┅┅ㄤ彐躅泔铎飙轸屙孱栳钽弪ㄩ翦愆钽镱轸屙祜镳鏖翳蜥铍骘蝈篚祠轭ㄧ弭骈屐候弩蹯趔轸屙骘殇ㄧ弭骈屐候弩蹯舡殇蝈篚祠骘筱矧戾舄è筱矧ㄩ翦憝麸泔铎轸屙后趄遽铋候弩蹯舡殇殇┅麸翎ㄧ弭骈屐呼雉犰筱矧濠ㄨ遽ㄧ弭骈屐鸿遽筱矧濠┅麒孱ㄡ钿麸翎桢徜ㄤ轹殇桢徜麸翎飑┅躅戾篌铛礅弪筱矧濠滹ㄦ矧磲泔铎飙轸屙孱栳钽弪ī铒筱矧镱轸屙蝈篚祠岍アㄧ弭骈屐洪殇轸屙ㄧ弭骈屐候弩蹯舡殇蝈篚祠┅犷蝈趱蝾铋屐箦滹瘐箬ㄡ泔铙横篚筱矧蝈篚祠蜥铍螬骈钺祆蝈趱蝾戾è蜥铍箫螋蜥铍＇弘妁＇灬礅溽蝈篚祠ㄧ弭骈屐横篚蝈篚祠┅┅ㄡ泔铙候犷塍祜镳鏖翳麸ㄧ弭骈屐横篚ㄦ轵篝蜥铍螬骘蜥铍轭蜥铍麒殪ㄧ弭骈屐横篚蜥铍麸皓泔祆邈ㄡ泔铙候犷蜥铍┅铋飑┅┅＋侯蹯祜镳骘轭Ж鞍氨安俺按暗岸胺案肮⒈阿⒈雹⒈并⒈尝⒈储⒈耽⒈盯⒈发⒈涪⒈耿⒉阿⒉雹⒉并⒉尝⒉储滹ㄤ锃轫痫螋轸屙ㄦ矧磲铋栾礤镥篁惘痿獐眚绡幄椹ㄦ矧磲铋Ⅳ弩舣黧掰幄椹烘矧磲吼翕┅＋侯蹯祜镳骘骝镯麸脖骘箬殒＇灬礅溽ㄩ洎ǐ殇舶鞍鞍鞍í卑鞍鞍┅滹ㄤ锃轫痫螋轸屙ㄦ矧磲铋栾礤镥篁惘泔铎彀汞孱肮铂О洚豇簪椹ㄦ矧磲铋Ⅳ弩舣泔铎忐铂О洧椹烘矧磲恒镱祆后栝骠箬殒舂＋侯蹯祜镳骘骝镯麸泊骘箬殒＇灬礅溽ㄩ洎ǐ殇舶鞍鞍鞍í卑鞍鞍┅滹ㄤ锃轫痫螋轸屙ㄦ矧磲铋栾礤镥篁惘泔铎彀矾肮铂О洚豇簪椹ㄦ矧磲铋Ⅳ弩舣泔铎忐铂О洧椹烘矧磲恒镱祆后栝骠箬殒舂