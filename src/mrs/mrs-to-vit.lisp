;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;   $RCSfile$
;;  $Revision$
;;      $Date$
;;     Author: Ann Copestake (CSLI),Walter Kasper (DFKI)
;;    Purpose: Converting MRS to VIT
;;   Language: Allegro Common Lisp
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; 
;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; Converting MRS (as lispified structures defined in basemrs.lsp)
;;; to VIT (as lispified structures defined in vit.lsp)

(in-package "MRS")

#|

here is an example of a lispified MRS (slightly faked compared to the actual grammar output
in order to illustrate some stuff succinctly)

#S(PSOA :HANDEL #S(VAR :NAME "h1" :EXTRA NIL :ID 1)
             :INDEX #S(VAR :NAME "e2" :EXTRA "(E:t3, R:t3, S:t4)" :ID 2)
             :LISZT
             (#S(REL :SORT PRPSTN_REL :HANDEL #S(VAR :NAME "h1" :EXTRA NIL :ID 1)
                     :FLIST (#S(FVPAIR :FEATURE SOA :VALUE #S(VAR :NAME "h5" :EXTRA NIL :ID 5))))
              #S(REL :SORT _EVERY_REL :HANDEL #S(VAR :NAME "h6" :EXTRA NIL :ID 6)
                     :FLIST (#S(FVPAIR :FEATURE BV :VALUE #S(VAR :NAME "x9" :EXTRA "(3SG_N)" :ID 9))
                             #S(FVPAIR :FEATURE RESTR :VALUE #S(VAR :NAME "h8" :EXTRA NIL :ID 8))
                             #S(FVPAIR :FEATURE SCOPE :VALUE #S(VAR :NAME "h7" :EXTRA NIL :ID 7))))
              #S(REL :SORT _DOG_N_REL :HANDEL #S(VAR :NAME "h10" :EXTRA NIL :ID 10)
                     :FLIST (#S(FVPAIR :FEATURE INST :VALUE #S(VAR :NAME "x9" :EXTRA "(3SG_N)" :ID 9))))
              #S(REL :SORT _CHASE_V_REL :HANDEL #S(VAR :NAME "h11" :EXTRA NIL :ID 11)
                     :FLIST (#S(FVPAIR :FEATURE EVENT :VALUE #S(VAR :NAME "e2" :EXTRA "(E:t3, R:t3, S:t4)" :ID 2))
                             #S(FVPAIR :FEATURE ACT :VALUE #S(VAR :NAME "x9" :EXTRA "(3SG_N)" :ID 9))
                             #S(FVPAIR :FEATURE UND :VALUE #S(VAR :NAME "x12" :EXTRA "(3SG_N)" :ID 12))))
              #S(REL :SORT TEMP_PREC_REL :HANDEL #S(VAR :NAME "h11" :EXTRA NIL :ID 11)
                     :FLIST (#S(FVPAIR :FEATURE EVENT1 :VALUE #S(VAR :NAME "t3" :EXTRA NIL :ID 3))
                             #S(FVPAIR :FEATURE LATER :VALUE #S(VAR :NAME "t4" :EXTRA NIL :ID 4))))
              #S(REL :SORT _SOME_INDIV_REL :HANDEL #S(VAR :NAME "h13" :EXTRA NIL :ID 13)
                     :FLIST (#S(FVPAIR :FEATURE BV :VALUE #S(VAR :NAME "x12" :EXTRA "(3SG_N)" :ID 12))
                             #S(FVPAIR :FEATURE RESTR :VALUE #S(VAR :NAME "h15" :EXTRA NIL :ID 15))
                             #S(FVPAIR :FEATURE SCOPE :VALUE #S(VAR :NAME "h14" :EXTRA NIL :ID 14))))
              #S(REL :SORT _CAT_N_REL :HANDEL #S(VAR :NAME "h16" :EXTRA NIL :ID 16)
                     :FLIST (#S(FVPAIR :FEATURE INST :VALUE #S(VAR :NAME "x12" :EXTRA "(3SG_N)" :ID 12))))
              #S(REL :SORT _BLACK_ADJ_REL :HANDEL #S(VAR :NAME "h16" :EXTRA NIL :ID 16)
                     :FLIST (#S(FVPAIR :FEATURE INST :VALUE #S(VAR :NAME "x12" NIL :ID 12)))))
             :H-CONS
             (#S(HCONS :SCARG #S(VAR :NAME "h5" :EXTRA NIL :ID 5)
                       :CANDS (#S(VAR :NAME "h13" :EXTRA NIL :ID 13)
                               #S(VAR :NAME "h6" :EXTRA NIL :ID 6)
                               #S(VAR :NAME "h11" :EXTRA NIL :ID 11))
                       :OUTSCPD NIL)
              #S(HCONS :SCARG #S(VAR :NAME "h8" :EXTRA NIL :ID 8)
                       :CANDS (#S(VAR :NAME "h10" :EXTRA NIL :ID 10))
                       :OUTSCPD NIL)
              #S(HCONS :SCARG #S(VAR :NAME "h6" :EXTRA NIL :ID 6)
                       :CANDS NIL
                       :OUTSCPD #S(VAR :NAME "h10" :EXTRA NIL :ID 10))
              #S(HCONS :SCARG #S(VAR :NAME "h15" :EXTRA NIL :ID 15)
                       :CANDS (#S(VAR :NAME "h16" :EXTRA NIL :ID 16))
                       :OUTSCPD NIL)
              #S(HCONS :SCARG #S(VAR :NAME "h13" :EXTRA NIL :ID 13)
                       :CANDS NIL
                       :OUTSCPD #S(VAR :NAME "h16" :EXTRA NIL :ID 16))
              #S(HCONS :SCARG #S(VAR :NAME "h13" :EXTRA NIL :ID 13)
                       :CANDS NIL
                       :OUTSCPD #S(VAR :NAME "h6" :EXTRA NIL :ID 6))))

The parts of the VIT that we are attempting to generate from this are:
1. Semantics
2. Main condition
3. Scope

We should also be able to generate

4. Tenseandaspect
5. Syntax

I ignore tense and aspect for now, since there may well be changes
Syntax looks a bit tedious but reasonably trivial, so I am leaving this to later

Here is what I think the target VIT semantics would be (ignoring the issues
involved in translation of relation names etc):

        :SEMANTICS (#S(P-TERM :PREDICATE PRPSTN_REL :ARGS (L1 H5))
                    #S(P-TERM :PREDICATE EVERY_REL :ARGS (L6 I9 H8 H7)) 
                    #S(P-TERM :PREDICATE DOG_REL :ARGS (L10 I9)) 
                    #S(P-TERM :PREDICATE CHASE_REL :ARGS (L11 I2))
                    #S(P-TERM :PREDICATE ARG2 :ARGS (L11 I2 I9))
                    #S(P-TERM :PREDICATE ARG3 :ARGS (L11 I2 I12))
                    #S(P-TERM :PREDICATE SOME_REL :ARGS (L13 I12 H15 H14)) 
                    #S(P-TERM :PREDICATE CAT_REL :ARGS (L161 I12)) 
                    #S(P-TERM :PREDICATE BLACK_REL :ARGS (L162 I12)))
        :MAIN-CONDITION L1
        :SCOPE (#S(P-TERM :PREDICATE IN_G :ARGS (L161 L16))
                #S(P-TERM :PREDICATE IN_G :ARGS (L162 L16))
                #S(P-TERM :PREDICATE EQ :ARGS (L10 H8))
                #S(P-TERM :PREDICATE EQ :ARGS (L16 H15))
                #S(P-TERM :PREDICATE EQ :ARGS (L13 H5))
                #S(P-TERM :PREDICATE EQ :ARGS (L6 H14))
                #S(P-TERM :PREDICATE EQ :ARGS (L11 H7))
                #S(P-TERM :PREDICATE LEQ :ARGS (L11 H7))
                #S(P-TERM :PREDICATE LEQ :ARGS (L11 H14))
                etc etc

what's happening here, beside the trivial rearrangement of structures is:
1. renaming variables - MRS x's and e's collapse to i, h's go to l's when they're
   labelling known rels.  Since we are effectively making the distinction between
   holes and labels in the MRS scoping stuff anyway, I have added a parameter to 
   the structure for handel variables to record if they are label handels
2. splitting the chase_rel into a Parsons style representation
3. changing the labels of black_rel and cat_rel so they are distinct
- this requires that we add grouping statements in scope -
old style would be group(L15,[L151, L152]), new style is in_g(L151,L15), in_g(L152,L15)
4. Adding scoping statements of the form LEQ or EQ
- in this example, the `outscoped' constraint has actually disambiguated the structure
Since the simplest technique (and possibly the only technique) for converting
the scope conditions currently used in MRS to VIT is to expand all structures and then
deduce the constraints, a plausible idea is to generate ALL
the possible LEQ and EQ statements.  Obviously this may not correspond to the output of a 
particular grammar - this however is going to be true of any mechanism.  (Even if
we use LEQs in MRS, the _precise_ leqs generated will depend to some extent on assumptions
about syntax.)  It's not worth devoting too much effort to sorting out the current situation,
because of the likelyhood of changes in the grammar.

|#
;;; hash table form semdbs:

(defvar *vit-semdb* (make-hash-table :test #'eq))

(defstruct semdbitem
  gramrel
  vitrel
  relsort
  args
  extra)

(defstruct vitarg
  gramrole
  vitrole
  vitsort)

;;; I assume that args is a list of the parameters 
;;; needs extension as the information becomes available from SemDB
(defun create-db-item (arglist)
  (make-semdbitem :gramrel (first arglist)
                  :vitrel (second arglist)
                  :relsort (third arglist)
                  :args (if (fourth arglist)
                               (loop for arg in (fourth arglist)
                                   collect
                                     (make-vitarg :gramrole (first arg)
                                                  :vitrole (second arg)
                                                  :vitsort (when (third arg)
                                                             (third arg)))))
                  :extra (subseq arglist 4)))


(defun insert-db-item (&rest args)
  (let ((item (create-db-item args)))
    (setf (gethash (semdbitem-gramrel item) *vit-semdb*)
      item)))

(defun get-db-item (key &optional (table *vit-semdb*))
  (gethash key table))


;;; **********  Globals  ***********


;;; *** `local' globals ****

(defparameter *group-members* nil)

(defvar *vit-instances* nil)

(defvar *hole-label-eqs* nil
  "store additional eqs for labels which must be holes according to vitADT")

(defparameter *used-handel-labels* nil
  "store handels which are used as base labels")

;;; **** General purpose utility functions ****

;;; general purpose functions for creating new atoms ...

(defun next (template)
       (let ((instance nil)
             (number (+ (or (get template 'last-number)
                            0)
                        1)))
         (setf (get template 'last-number)
               number)
         (setf instance
               (intern
                 (concatenate 'string
                              (string template)
                              (princ-to-string number))))
         (push instance
               (get template 'children))
         (setf (get instance 'root-template)
               template)
         instance))

;;; and destroying them
;;; (not used at the moment, so get different numbers every time the groups are
;;; constructed)

(defun scratch (templates)
;;; see the function Next - scratch removes all info from the template symbols
;;; used by Next and in effect reinitialises the values.  It can take a single
;;; item or a list of templates to be reinitialised.
    (dolist (template (if (listp templates) templates (list templates)))
	    (remprop template 'last-number)
	    (dolist (child (get template 'children))
		    (setf (symbol-plist child)
			  nil))
	    			; a bit drastic - have to hope nothing
				; important is kept on p-list by anyone else
	    (remprop template 'children)))

(defun clear-temporary-dbs ()
  (setf *group-members* nil
        *vit-instances* nil
        *hole-label-eqs* nil
        *used-handel-labels* nil))

;;; here with fixed package
(defun p-symbol (&rest args)
  "Concatenate symbols or strings to form an interned symbol"
  (intern (format nil "狺狎珞⑼矣┅换痱邃轶狍篚礤麸忮簌礅镬轭翳珧犴磲滹磲轭换狒痱弩孱翳穆滹铒狃疱狎狃痱镳蜷狒麸翳瘐蝠矬ㄤ彐躅轭箦螋痱邃殂狒瀛趄犷箪狒轱痱邃痱邃镳糸镱犰ㄤ镯衢翡飙箬秣沲蝌孱舡滹磲轭┅戾è滹眇蝈ㄦ轭洵簌礅镬篝蜷铉痱邃暴滹磲轭┅箦翩ㄧ弭栳箬ㄩ滹眇蝈滹眇蝈痱邃暴鲩舡箦礓猹痱邃博┅换翎忪祜镫躔铒汨彘钼狨孱ㄤ彐躅珏舡鲩舡痱邃殂狒瀛钺礤蝈祛犴濠戾è溻轸屙ㄧ弭溻轸屙蝈祛犴濠┅ㄩ溻轸屙箦礓忾翦憝鲩趄屐溻轸屙戾舄è蝈毂ㄩ箦憝蝈灬糸镱篚骀轼蝈盹鲥钺礤篚骀轼蝈祛犴箦憝蝈灬糸镱篚骀轼蝈祛犴濠蝈觳ㄩ箦憝蝈灬糸镱痱彐轼蝈盹鲥钺礤痱彐轼蝈毂箦憝蝈灬糸镱痱彐轼蝈毂┅篝蜷铉趄轫蝈觳┅┅ㄤ彐躅泔祆邈舡鲠祯弩骝镯蝈蝈飑换蝈趱蝾翳鲠祯弩骝镯蝈灬糸镱雉桢翳犷翳栳钿屐换铒翦翳狒翳轶蝈趱蝾翳孱糸蝈篝蝓泗躜弩铄沐篌狎忮汜躞换镦殇孱糸骈汜糸镱镦箫螋镦鲠蜷徕戾螬ㄦ矧骣轭蝈飙骒轶蝈飑泔祆邈ㄦ鲳衢颦鲠祯骣皓┅ㄤ彐躅珏舡狎绛蝻戾鲠颟ㄩ鲠颦鲠颟ㄣ镱è眚蟓灬铉踽珏Ж珏蝽犷赆疳铄箦┅戾è翳蝻戾ㄦ轭翳蝻戾驽狒躜濯鲠颦屮趄鲠颟弘妁＇骣疳轵驽狒躜濠┅ㄩ翳蝻戾ㄦ鲳衢颦鲠祯翳蝻戾┅┅铋飑┅ㄤ彐躅泔矧洵鲠颦鲠颟ㄡ钿鲠颦鲠颟ㄥ耢ㄥ祠鲠颦钺礤鲠颟癌В茔┅换骝镯尼瞌驳腐狗鲥蝮轱换惋溟骈邃泔祆邈舡狎珞犷洵鲠祯弩骝镯蝈麸骈祠弪秕镳糸镱犰狎珲礤铘换鲠蜷徕戾翳狒麇蝈铒骈祆邃澡弩鲠蜷徕戾狎犰麽玳鲥钺礤换忮玳铑轭鏖翳Ⅵ箫翳轶孱泔溟铉轶翦篝邃桢蝈ㄤ彐躅镳糸镱犰鲠颦鲠颟ㄡ钿鲠颦鲠颟ㄥ耢ㄥ祠鲠颦钺礤鲠颟癌В荟┅ㄤ彐躅泔祆邈舡狎珞犷洵鲠祯弩骝镯蝈蝈飑换蝈趱蝾翳鲠祯弩骝镯蝈灬糸镱雉桢翳犷翳栳钿屐换箴扉躔轭翳矬骘狎绛蝈灬糸镱犷铒蝽犰换铒翦翳狒翳轶蝈趱蝾翳孱糸蝈篝蝓泗躜弩铄沐篌狎忮汜躞换镦殇孱糸骈汜糸镱镦箫螋镦鲠蜷徕戾螬戾è狎珞铋飑雉桢蝮铋飑祜镳骘骣轭蝈飙骒轶蝈飑滹戾舄è鲠祯ㄦ鲳衢颦鲠祯骣皓ㄦ遽ㄦ鲳衢颦驽狒躜骣皓ㄤ彐ㄡ篌镢驽狒眚蟓狎绛驽狒躜弩┅ㄡ蜱ㄩ溴矧ㄡ钿ㄣ镱箴蝈篝溴姗ㄦ轵篝蝈篝溴姗┅ㄧ弭狎绛蝻戾鲠祯濠蝈篝溴姗驽狒┅┅换矧忮趑弪躞骣鹂靠躅戾篌ㄡ钿镳糸镱犰鲠颦鲠祯濠眚蟓灬铉踽珏Ж孱珈轶瑭┅ㄩ狎换狎珞狍箫悱涕篝ㄡ蜱鲠颟箦翩狎珞ㄡ泔铙狎鲠祯狎珞┅箦翩雉桢蝮ㄡ痧孱雉桢蝮扉篝ㄦ鲳衢颦鲠祯骣皓┅┅┅扉篝狎珞雉桢蝮┅ㄤ彐躅泔祆邈舡犰飙栳钿屐鲠蝮蝈祗换情鲥箦镦蝈灬糸镱蟋翳轶蝈趱蝾翳扉篝镦灬忮祗犷镦换珧秕痼ㄩ瀹灬忮祗麒殂趱蝾躔盹蝈翳犷镱沐换忾蝈漉钿犷衄玳鲥翳狒麇蝈躞轭翳筱镳轭泔溴换怩滹瞌麽铘麸礤篌鏖翳翳狒躅铄沐篌狎殪换灬忮飙扉篝铒轶狍箫悱扉篝ㄨ犷溴灬忮飑换姿铒麇趄遽弼弪栳钿屐狍珧秕戾è灬忮飙扉篝铋飑ㄧ蝻躔扉篝铋飑ㄥ沐痿扉篝铋飑祜镳骘蝈轭蝈祗滹戾è鲠蝈飙栳钿屐蝈飑灬忮蝈飙灬忮蝈飑箴蝈ㄡ篌镢蝈飙箫螋蝈飑鲰箴邈獒飙灬忮飙栳汶扉篝┅躅戾篌ㄩ蟓栳钿屐鲠鲠颟ㄥ蝌矧ヒ屐狒轱栳轭泔蝌邈栳钿屐立蝈飙箫螋蝈飑鲠颟换ㄣ镱è狍箫鲠颦殇鲠颟灬忮飙扉篝换瘐箬铄鲠颦殇鲠颟珧秕瓠扉篝┅换è珧秕瓠鲠颦鲠颟换瘐箬铄鲠颦殇鲠颟珧秕瓠扉篝┅换舂麒孱鲠颦鲠颟瘐箬铄ㄣ镱鲠颦殇鲠颟ㄩ鲠颦灬忮飑鲠颦殇灬忮飑┅灬忮飙扉篝ㄩ箴蝈戾舄è狎铘蝈篝箴蝈飑蝈飙骒轶蝈飑┅麒孱ㄩ蟓栳钿屐鲠ㄦ鲳衢颦鲠祯狎绌瘐箬鲠颦殇ㄦ鲳衢颦鲠祯狎绌屮沐痿扉篝箦翩珧秕瓠扉篝ㄤ屐弭鲠颦殇ㄦ鲳衢颦鲠祯狎绌珧秕瓠扉篝┅┅ㄩ礤礅弪鲠颦殇鲠颟屮沐痿扉篝瘐箬铄鲠颦殇鲠颟珧秕瓠扉篝┅┅鲠祯弩灬忮飙扉篝珧秕瓠扉篝┅ㄤ彐躅泔祆邈舡犰飙轭篝犷沐鲠蝮扉篾舂戾è轭篝犷沐螬祜镳骘蝈轭扉篾滹祜镳骘鲠轭蝈飙骒轶蝈飑滹ㄩㄡ钿鲠颦鲠颟铒ㄩ蟓栳钿屐鲠鲠颟┅瘐箬铄鲠轭篝犷沐螬┅轭篝犷沐螬换歪轭泔溴换眚蟓麸鲩舡骈戾⑼徙轭麸箬饶红脞苟喉蝮喉蝮鲩舡翦篝螈ㄤ彐躅眚蟓麸鲩舡骈戾ㄦ殪瀛钺礤鏖翳镳孱骈戾ㄩ篝蝈犴骈戾钺礤轰轵邈糸镱洪铕豸ㄤ铼è箦铘孱沐蝈徜轶趄遽铋у镦蝈徜轶趄遽铋у镦┅眚篌趄蹉蝈徜轶趄遽铋у镦蝈徜轶趄遽铋у镦┅è矧ㄥ耢箦铘孱沐у镦ㄥ耢眚篌趄蹉у镦┅铋飑麒孱ㄡ钿眚篌趄蹉痼镝眚篌趄蹉舂眭祠轲戾鲠祯瀛忾钿鲩忾钿轭绛箦趔眚蟓麸鲩眚篌趄蹉舂箦翩汜铒铋汜飙忾钿轭珞铋飑ㄦ矧磲立箦铘孱沐ㄦ矧磲テ芋秕麴豸眚眚篌趄蹉箝眇戾ㄦ矧磲フ铙泔疱骘蝽秕麴豸眚眚篌趄蹉ч钿屮邃换翳孱趄犷骈钿箦趔镦忾钿轭珞麒殂鏖祆玳鲥骢祆筱镳邃换篝蝓泗躜瀣犷秕麴豸翳蝈篚祠ㄩ忾钿轭绛箦趔ㄦ矧磲ビ泔疱骘蝽螬ㄦ矧磲プ烈紊吻深鲠扉鸵篝蝓泗躜澧┅ㄦ矧忾钿轭轭忾钿轭绛箦趔滹箦翩汜铒铋汜飙忾钿轭珞ㄣ犷镱殂犰忾钿轭珞忾钿轭绌秕麴豸泔铑邈翦洵眚眚篌趄蹉ч钿屮邃秕麴豸筱镳邃眚眚篌趄蹉舂麒孱鲩黩轸瀛鲩舡痱弭豉ㄨ矧蜷忪瀛栳汶鲩舂┅ㄦ矧磲オ┅┅┅换骢钽糸镱麸汜祆骝镯辛桥矧趟轭翦蜴徙ㄤ彐躅眚蟓麸鲩舡泔铞弪眚蟓痼镝镳糸镱犰篝犷溽祜铄舂ㄩㄥ眚蟓骘颦灬铉踽珏у铉扉箬戾è眚篌趄蹉ㄩㄢ秕钿И矧溴蝈洵眚蟓蝓戾扉篝眭铉瀛眚蟓篝蝓泗眚蟓痼镝矧溴蝈洵眚蟓蝓戾扉篝眚蟓痼镝┅眭祠轲戾鲠祯瀛忾钿鲩忾钿轭绛箦趔眚蟓麸鲩眚篌趄蹉舂箦翩汜铒铋汜飙忾钿轭珞铋飑麒孱篝犷溽祜铄ㄦ矧磲フ铙泔疱骘蝽秕麴豸眚眚篌趄蹉ч钿屮邃换翳孱趄犷骈钿箦趔镦忾钿轭珞麒殂鏖祆玳鲥骢祆筱镳邃换篝蝓泗躜瀣犷秕麴豸翳蝈篚祠ㄩ忾钿轭绛箦趔ㄦ矧磲筱镳邃骘蝽螬立戾铉翳忾钿轭绛箦趔ㄩ戾铉翳忾钿轭绛箦趔卑镱禊痱轭糸铉骈蝮卑┅ㄦ矧磲プ烈紊吻物鲠扉筱镳弩轭鲠扉鸵篝蝓泗躜┅ㄦ矧忾钿轭轭篚怏羼忾钿轭绛箦趔黹戾铉翳忾钿轭绛箦趔卑┅滹箦翩汜铒铋汜飙忾钿轭珞ㄣ犷镱殂犰忾钿轭珞忾钿轭绌秕麴豸泔铑邈翦洵眚眚篌趄蹉ч钿屮邃秕麴豸筱镳邃眚眚篌趄蹉舂┅麒孱ㄡ钿鲩篝犷溽祜铄黩轸瀛鲩舡痱弭豉ㄨ矧蜷忪瀛栳汶鲩舂ㄦ矧磲ア┅ㄣ桢汶鲩鲩舂鲩舂戾è鲩ㄧ弪磲瞽眚蟓麸鲩眚蟓痼镝┅麒孱篝犷溽祜铄ㄦ矧磲フ铙泔疱骘蝽秕麴豸眚眚蟓痼镝ч钿屮邃┅麒孱ㄡ钿鲩篝犷溽祜铄黩轸瀛鲩舡痱弭豉鲩舂ㄦ矧磲ア┅鲩舂┅ㄤ彐躅汨邈氕鲩鲩镳糸镱犰ㄡ蟓篝蜷铉铋飑篝蝈犴篝犷溽蜾秕麴豸┅鏖翳镳孱骈戾鲩舡秕繇鸠鲩翥桢汶轰轵邈糸镱猴豸瘐洪姝屮轶趔后躔弪箦溴ㄦ矧磲鲩舡秕㈠铙躜暹祜徜邃鲩袅脑┊ブㄩ狍篝蜷铉ㄦ矧磲鲩舡秕立鲩舂黩轸瀛鲩鲩舡秕鲩舂ㄦ矧磲鲩舡秕鲩裘桢汶ㄖ┊ヨ犰舢ア┅ㄥ沆汉蝓瞽箬屐飙泔眄犷沅屣灞鲰帛鲩袅脑扉獐珠暨龄艋镳舣聃轭趱蟑忾畛帛篚畲弹痱镬镧繇鸠鲩翥桢汶猴豸瘐繇鸠鲩麸豸洪姝秕麴豸屮轶趔后躔弪箦溴哄蝌矧秕麴豸繇鸠鲩翦蝌矧洪姝弪蝻颦秕麴豸屮轶趔后躔弪箦溴ㄥ沆汉蝓瞽箬屐飙泔眄犷Ⅳ衢刀繇鸠鲩翦蝌矧翎殪翎殪翎殪颌猴豸瘐篝蝈犴ㄦ矧磲篝蝈犴ア┅ㄤ彐躅栾蝌殁戾栳汶鲩舂换溴戾翦犷戾耋麒殂汜铑雉忮屮痱弩箦泔蝌邈綮忮汜躞换翳箦泔钿狎珲礤铘栳忮孱篝轲蹯狒邃麸忮灬忮戾舄è筱镳鲩舡筱镳鲩舂铄鳝筱镳ㄦ矧轸屙轭筱镳骈祠弪戾舄è狎珞瓠翦蝽狎珞轸屙┅ㄩ矧铒ㄥ耢瓠翦蝽痱邃殂狒轸屙ъ羼┅ㄡ钿ㄣ狎狎珞ㄣ徜狎珞ㄣ栳颦羼踽ㄥ祠篝蜷铉ㄣ狎狎珞┅癌＼飑ㄣ栳颦羼踽ㄥ祠篝蜷铉ㄣ徜狎珞┅癌＼瑭┅轸屙┅┅箦翩鲩舡筱镳鲩舂铄鳝筱镳濠鲩舂ㄤ彐躅眚蟓麸鲩鲩趄殒殄洵眚螬换骈蝮麇痱镤蹉犰筱镳邃篝蝓泗躜弩躞轭翳泔溴轭眚篁弩镬鲥祗换麇犰箫泔祆邈躔犰翳鲠篝蝓泗躜弩骘翳栳钿屐ㄣ戾狎翦眇矧狎溻螬箦翩沲蝌孱舡鲩舄磲脲鲩舂戾舄è躅篝蝓铉痼镝眚蟓躅篝蜷铉痼镝鲩趄殒殄洵眚螬洛汜躞稚篝蹑滹弩瞌扉脲篝蜷铉螽澡镬泔溴狍篚礤篝蜷铉豉疱黠蹯忮泔铞弪翦麸簌礅镬镱泔铙趄蹉糸镱镦翳鸵蝇怩翳轶祜箦轭骘蝽狒轱翳轶骢钽糸镱犰箫泔铞弪趔翳疳翳鲠祯弩轭翳噱趄岌痱镳弪豉扉篝轭麸箝铉戾驽狒躜鲠祯弩眚蟓痼镝ㄩ眚蟓灬铉踽珏Ж孱珈轶瑭糸礤泔铞弪舡眚蟓篝蝓泗躅篝蝓铉痼镝躅篝蝓铉痼镝┅ㄢ轭溟铉箦趔磲脲筱镳邃眚眚蟓痼镝┅ㄥ聃犰轸殄螬戾耋┅眭祠轲戾鲠祯瀛忾钿灬忮飙鲠蝮珧秕瓠扉篝ㄣ镬戾泗犰飙栳钿屐鲠蝮痼镝扉篾眚蟓痼镝┅换翳孱麇黠螂秕麒殂羼踽扉糸弩犷戾耋狎泔眄镱麸换犰镦翳筱镳邃篝蝓泗躜弩麒孱忾钿轭绛箦趔眭祠轲戾鲠祯瀛箦赳ㄥ聃犰轸殄戾耋黠螂秕舡筱镳轭绛蝈篝蜷泗轱铙眚蟓痼镝ㄣ狎忾钿轭绛箦趔┅ㄦ矧忾钿轭绛箦轭ㄣ潋忾钿轭绛箦趔滹眭祠轲戾鲠祯瀛忾钿铄鳝羼铄鳝戾耋黠螂秕舡筱镳轭绛蝈篝蜷泗轱铙眚蟓痼镝忾钿轭绛箦舂箦翩羼踽扉糸弩ㄤ屐弭瀛殒铒＇灬礅溽ㄥ聃犰轸礤礅弪羼踽扉豉铄鳝羼呼弩＇灬礅溽铄镬洎矧ㄥ聃犰铄镬洎ㄡ钿ㄥ耢ㄣ狎铄鳗ㄣ潋镬洎ㄥ耢ㄣ潋铄鳗ㄣ狎镬洎┅┅┅羼踽扉糸弩┅箦翩戾耋ㄤ屐弭瀛殒铒＇灬礅溽戾瘵礤礅弪戾铄鳝戾耋呼弩＇羼踽飑戾耋┅┅戾舄è灬忮祗ㄩㄡ篌镢鲠颦殇痼镝栳钿屐眚蟓痼镝┅灬忮飙鲠蝮灬忮飙鲠蝮瘐箬扉篝鲠颦殇痼镝栳钿屐眚蟓痼镝┅灬忮飙鲠蝮┅蝈祗痼镝扉篾眚蟓痼镝┅筱镳ㄣ镱篝蝓泗鲩舡筱镳羼踽扉糸弩戾耋灬忮祗┅ㄣ镱鲥螋邃蝈祗ㄣ镱鲥螋眚蟓蝈祗麸鲩蝈祗沲蝌孱舡鲩舄珧秕瓠扉篝灬忮祗┅换珧秕瓠礤礅弪螵轶泔铙趄蹉翦狍箝溴彐驽泗镦泔铞弪糸铉蝈祗换轭翳孱珈轶珧犴磲翳ы弩筢珏轶轭沆蹁邃轭扉篾盹镤铋飑ㄧ蝻躔ㄣ镱篝蝓泗鲩舡珧秕痼珧秕瓠礤礅弪螵┅箦翩鲩舡豸翦蜥钽瀛殇沲蝌孱舡鲩舄ㄣ镱篝蝓泗箦珥孱舡溴筱蜷痿轱眚蟓痼镝鲩舡磲轭泔钿轸轱沲蝌孱舡鲩舄ㄣ镱篝蝓泗磲轭灬忮眚蟓痼镝沲蝌孱舡鲩舄盹镤灬忮祗鲩舡筱镳沲蝌孱舡鲩舄ㄡ痧孱珧秕痼筱镳鲩舡筱镳沲蝌孱舡鲩舄┅鲠祯弩沲蝌孱舡鲩舄忾钿轭绛箦趔┅┅ㄤ彐躅珏蝽犷眚蟓麸鲩眚螬ㄣ戾狎翦眇矧狎溻螬箦翩沲蝌孱舡鲩舄磲脲鲩舂眭祠轲戾鲠祯瀛忾钿灬忮飙鲠蝮珧秕瓠扉篝ㄣ镬戾泗犰飙栳钿屐鲠蝮ㄩ痼镝礤篌徵眚螬ㄣ镱痼镝礤篌徵眚螬痼镝扉篾眚螬痼镝扉篾眚螬┅戾舄è灬忮祗ㄩㄡ篌镢鲠颦殇痼镝栳钿屐眚螬灬忮飙鲠蝮灬忮飙鲠蝮瘐箬扉篝鲠颦殇痼镝栳钿屐眚螬┅灬忮飙鲠蝮┅ㄩ铙舡鲠蝮ㄣ镬戾泗犰飙轭篝犷沐鲠蝮痼镝扉篾眚螬┅蝈祗痼镝扉篾眚螬ㄣ镱鲥螋邃蝈祗ㄣ镱鲥螋眚蟓蝈祗麸鲩蝈祗沲蝌孱舡鲩舄珧秕瓠扉篝灬忮祗┅筱镳ㄣ镱鲥螋鲩舡筱镳痼镝璀泔铙眚螬沲蝌孱舡鲩舄灬忮祗┅盹镤ㄩㄡ钿痼镝礤篌徵眚螬铒礤礅弪蝈飙箫螋痼镝礤篌徵眚螬鲰殓铒蝈洵箦铘孱沐盹镤┅ㄦ轵篝ㄣ镱鲥螋眚蟓蝈飙麸鲩痼镝礤篌徵眚螬沲蝌孱舡鲩舄珧秕瓠扉篝灬忮祗┅┅ㄧ蝻躔ㄣ镱篝蝓泗鲩舡珧秕痼珧秕瓠礤礅弪螵┅箦翩鲩舡豸翦蜥钽瀛殇沲蝌孱舡鲩舄ㄣ镱篝蝓泗箦珥孱舡溴筱蜷痿轱眚螬鲩舡磲轭泔钿轸轱沲蝌孱舡鲩舄ㄣ镱篝蝓泗磲轭灬忮眚沲蝌孱舡鲩舄盹镤灬忮祗鲩舡筱镳沲蝌孱舡鲩舄ㄡ痧孱珧秕痼筱镳鲩舡筱镳沲蝌孱舡鲩舄┅沲蝌孱舡鲩舄┅换骘滞箝泔礤骝镯翳疳蝮弪犷轶篝矧邃轭箦珥孱舡殇ㄤ彐躅泔铙趄蹉舡箦珥孱舡溴筱蜷痿轱眚螬磲脲瓠翦蝽吼蝈溟汜翦Ⅵ轸赡横蜱扉篝ㄩ箦珥孱舡殇箦珥孱舡殇戾è灬铉ㄣ狍眚蟓骘颦灬铉踽珏ㄧ弪磲Ж珏簌铘狲珏颟ㄥ铉扉箬Ж孱簌铘狲孱绌赆疳铄箦Ж牮簌铘狲赆皓Ж溴簌铘狲珏颟┅┅磲脲箝后秕蜚屐犷珲徵ㄦ轵篝灬铉恒躜蝈铘灬铉踽珏ㄦ轵篝灬铉后孱溴箦泔钿灬铉┅┅ㄣ镬戾泗麒绛殇灬忮祗痼镝麋扉篾眚螬┅┅ㄤ彐躅泔祆邈舡麒绛殇灬忮祗麋扉篾舂ㄩ麋扉篾戾è蝈铋飑祜镳骘麋轭麋扉篾滹戾舄è殇麒绛殇殇麋┅灬忮祗祜镳骘轭麒绛殇栳钿屐麋泔祆邈戾è栳钿ㄩ躞邃栳钿屐灬忮祗蜥篌镢鲠颦殇飑躞邃栳钿屐灬忮祗┅┅ㄩ栳钿ㄩ铘弪ㄦ矧磲铋⑻立ㄦ轵篝栳钿┅ㄩ铘弪ㄦ矧磲铋⑻立鲠颦殇飑┅┅┅ㄥ轶麴ㄦ轭殇蝈弘妁＇麒绛殇殇┅ㄩ屮轶麴箦翩麒绛殇栳钿屐屮轶麴躅轱麒绛殇栳钿屐屮轶麴灬忮祗┅瘐箬磲脲麒绛殇洪殇瑚矧麒绛殇黠蜾麋鸿犷溴灬忮祗蝈螬┅蝈螬┅换麇汜徜犷戾癔羼泔铙趄衢铘磲轭灬忮麸痂犷溴殒麇扉脲换狒痱弩孱翳麸痂犷溴轶蝈玑蜾邃狍灬忮轭篝遽狍栾戾换麇犰箫趄遽箦铘孱沐盹镤礤篌徵濠桢蝈ㄤ彐躅泔铙趄蹉舡磲轭灬忮眚鲩盹镤灬忮祗戾舄è麸痂犷溴ㄣ镱鲥螋眚蟓鲠飙麸鲩痼镝麸瓠眚螬灬忮祗┅磲轭灬忮ㄣ镱鲥螋眚蟓鲠飙麸鲩痼镝栳钿屐眚螬灬忮祗┅ㄩ钿屮ㄣ镱鲥螋眚蟓鲠飙麸鲩痼镝轭溴眚螬灬忮祗┅麸痂镬麸痂犷溴飑戾磲脲瓠翦蝽吼蝈溟汜翦ъ羼横蜱扉篝磲轭灬忮麸痂犷溴飑┅ㄣ镱è犷盹镤戾铉翳瓠翦蝽狎珞盹镤┅博箦翩麸痂镬ㄧ弭珧秕瓠镦灬忮ㄦ轵篝瓠翦蝽狎珞盹镤┅珧秕瓠礤礅弪螵┅瘐箬盹镤鲩舡箦磲铘殂鲩舂┅换è泔铙鲩舡箦磲铘殂鲩舂换箦翩麸痂镬ㄦ轵篝瓠翦蝽狎珞ㄦ轵篝鲩舡箦磲铘殂鲩舂┅┅铋箦翩麸痂镬ㄩ铘弪ㄦ矧磲铋⑷立ㄦ躅汜祆鲠蜷徕戾珏铄蜥麸颡┅┅┅磲脲瓠翦蝽吼蝈溟汜翦ч钿屮横蜱扉篝麸痂镬磲轭灬忮轭溴┅┅换骘清蝽犷蝈盹鲥戾瘰鏖翳栾戾轭骈蝮痫箝糸镱翳妁箬秕熹换泔蝌弩痫钿麸躅怙躅栾戾骘麒殂铒泔钿轸轱屮轶趔换祜玳汜祆翳弪轶铒铄邃麸趄遽翳轶怩珲弩翳鲩袅脑黠蹯换泔眇灬轭狒痱弩孱ㄤ彐躅泔铞弪舡鲩舡筱镳ㄨ泔铙鲩灬忮祗祜镳骘戾轭桡镱钽镱麒孱戾癍筱戾瘵戾舄è蝈灬糸镱蝈盹鲥钺礤篚骀轼戾癍筱蝈灬糸镱戾瘵⑦筱┅灬忮灬蜱ㄣ镱鲥螋眚蟓鲠飙麸鲩戾癍筱筱狎戾瘵灬忮祗┅ㄨ犷溴灬蜱ㄩ篝蜷铉蝈灬糸镱㈠瘼ㄣ镱鲥螋栳钿屐麸鲩戾癍筱秕趔沭戾瘵灬忮祗ㄣ镱鲥螋栳钿戾麸栾戾戾癍筱秕趔沭戾瘵鲩灬忮祗┅┅ㄩㄡ钿ㄥㄥ祠簌礅镬钺礤灬忮灬蜱癌＼醛眚蟓灬铉踽珏Ж珏蝽犷┅铋扉篝磲脲瓠翦蝽吼蝈溟汜翦蝈灬糸镱横蜱扉篝灬忮灬蜱栳钿屐狎绌┅┅┅ㄤ彐躅珏舡鲩舡轭篝犷沐骝镯蝈痿弪愆换狍篚礤翳狒轭篝犷沐轶犰麽箦泔钿狎骘矧溟钺蝙蝈灬糸镱箦泔钿瓠翦蝽狎珞痿弪愆┅换泔铞弪箝镱镦蝈灬糸镱换翳轶轶鲥蝙箝眇戾狒翳盹礤铘犷鏖祆栳鲥麸忮盹溟骈邃轭铛礅弪镦麽换姿蝈祗汜骈祆箦鲥蜥鲩舡箪雉蠛箦磲铘殂蟋簌铘狲溟筱秕蝮瀣箫螋ㄤ彐躅泔铞弪舡眚蟓蝈祗麸鲩蝈祗鲩珧秕痼灬忮祗祜镳骘蝈轭蝈祗滹戾舄è蝈灬糸镱蝈飙箫螋蝈飑箴邈獒ㄡ篌镢蝈灬糸镱箴邈獒飙豉疱趄遽繇孱舄┅ㄣ镱è礤礅弪蝈灬糸镱滹铒舡泔铞弪舡箫螋扉篝铋飑箴邈獒ㄦ躅汜祆蝈篝箴邈獒飑蝈鲩珧秕痼灬忮祗┅箦翩鲩舡箦磲铘殂鲩舂ㄡ痧孱鲩舡箦磲铘殂鲩舂ㄣ镱鲥螋眚蟓蝈飙麸鲩蝈鲩珧秕痼灬忮祗┅┅┅┅换惋溟纟泔铞弪舡眚蟓蝈飙麸鲩麸犰祜骘箴邈獒蝻戾扉脲纳麒殂狎换趄遽翦嗅蝮镱蟓篝戾鏖翳箬狎邃灬忮飕怩麒殂滹铒轭沆蹁翳换轭篝犷沐鲠蜷徕戾镦翳痱邃殂狒翳妁泔礤骝镯烷玷犰箫忮躞邃骘换篝狒唑屐殒麇溴汩溴麸轭沆蹁胖盼蝻戾轭痱邃殂狒轹行蟋列蟋弭惝ㄤ彐躅泔铞弪舡眚蟓蝈飙麸鲩蝈鲩珧秕痼灬忮祗换蝈趱蝾扉篝麸犰祜骘箴扉趑轭镦蝈灬糸镱轭麸眭祠轲戾瓠翦蝽换瀹绠骘鲥蜮戾舄è灬忮ㄣ镱鲥螋灬忮飙麸鲩蝈飙栳钿屐蝈飑珧秕痼灬忮祗蝈飙灬忮蝈飑┅ㄡ蜱ㄣ镬戾泗狎珞犷洵鲠祯弩骝镯蝈蝈飑痱邃磲脲瓠翦蝽吼蝈溟汜翦ㄧ弭鲩舡痱邃殂狒瀛钺礤蝈飙箫螋蝈飑横蜱ㄣ镱灬忮祜镳骘鲠轭箦泔钿狎珞泔祆邈ㄣ镱鲥螋眚蟓鲠飙麸鲩鲠灬忮祗┅┅ㄩ铙ㄧ弭鲩舡轭篝犷沐骝镯蝈痱邃┅箦磲铘殂ㄣ镱痱邃祜镳骘狎轭ㄦ轵篝狎珞换狎轶ㄡ蜱鲠祯濠泔祆邈磲脲瓠翦蝽吼蝈溟汜翦ㄦ轵篝狎绌横蜱ㄩ礤礅弪ㄦ轵篝狎绌铒轭篝狎绛蝻戾螵扉篝灬忮ㄣ镱鲥螋眚蟓鲠飙麸鲩蝈篝狎绌灬忮祗┅扉篝灬忮轭篝ㄣ镱鲥螋眚蟓鲠飙麸鲩蝈篝狎绌灬忮祗┅┅┅┅ㄣ镱鲥螋眚蟓鲠颦屮趄箦泔钿狎珞鲩轭篝珧秕痼灬忮祗麒孱蝈飙屮趄蝈飑ㄣ镱鲥螋眚蟓蝈飙屮趄蝈飙屮趄蝈飑鲩轭篝灬忮珧秕痼灬忮祗┅麒孱蝈灬糸镱豉疱汨邈氇ㄣ镱鲥螋蝈灬糸镱豉疱轭骘麸鲩蝈鲩轭篝灬忮飑箦磲铘殂螬换翳珧秕鸠灬忮殳栾戾溟篝轭泗轱铄邃蝈黠螂轭ㄤ彐躅泔铞弪舡眚蟓鲠飙麸鲩鲠灬忮祗换殓铒蜷铉珧秕痼骘翳盹礤铘换骘鲠蜷徕戾翳轶轶箝眇禊磲趑弪镦痱秭殇轭翳泔蝌邈换戾趑弪狃疳蝈铘禊翳轶轶换骘栾戾换骘灬忮祗换骘弼弪翳轭屐箦换骑泔铌躅泗轱铙狒戾狍衄麇磲栳鲥扉篝镦鲠蜷徕戾换麇磲犰箫栳鲥泔铙翎铘ㄣ镱è扉篝鲠飑ㄦ矧篚怫犰轭鲠泔祆邈ㄣ镱鲥螋眚蟓鲠飙麸鲩篚怫犰灬忮祗┅è鲠颦鲠飑ㄣ镱è轶栳钿屐鲠鲠飑ㄣ镱鲥螋栳钿屐麸鲩鲠灬忮祗┅è泔矧洵鲠颦鲠飑ㄣ镱鲥螋眚蟓鲠飙麸鲩ㄦ鲳衢颦鲠祯ㄦ轭ъ轶鲠颦屮趄鲠飑弘妁＇骣疳轵驽狒躜濠灬忮祗┅ㄩ铘弪ㄦ矧磲铋⑸立鲠颦殇鲠飑┅┅è轶麸瓠豉疱鲠飑铋飑鲠飑┅ㄤ彐躅泔铞弪舡栳钿屐麸灬忮鲠飑ㄩ鲠颦鲠飑ㄩ铘弪ㄦ矧磲铋⑻立鲠颦殇鲠飑┅┅ㄤ彐躅泔铞弪舡栳钿屐麸鲩鲠灬忮祗ㄩㄡ篌镢鲠颦殇鲠飑灬忮祗ㄩ铘弪ㄦ矧磲铋⑻立鲠颦殇鲠飑┅ㄩ铘弪ㄦ矧磲铋⑷立鲠颦殇鲠飑┅┅ㄤ彐躅泔铞弪舡灬忮飙麸鲩鲠珧秕痼灬忮祗镳糸镱犰灬忮铋飑戾舄è殇鲠颦殇鲠飑扈疳轵ㄩㄡ钿灬忮鲠颦灬忮飑ㄦ轭ㄣ镱殇鲠颦殇灬忮飑灬忮祗呼弩＇羼踽飑ㄡ篌镢殇灬忮祗┅┅躅戾篌扈疳轵ㄣ弪蝻⒃蝈狒狍灬忮犷麽⒅犰轶篚痧矬邃麸忮灬忮怩轶铒镱灬忮扉篝鲠飑ㄩ礤礅弪殇珧秕痼戾è铄鳝鲠ㄣ镱è犷灬忮蝈篝扈疳轵┅ㄩ铘弪ㄦ矧磲铋⑻立蝈篝扈疳轵┅┅ㄩ铘弪ㄦ矧磲铋⑻立ㄦ躅汜祆鲠蜷徕戾珏铄蜥麸颡┅┅痱弼轱躞礤礅弪ㄡ篌镢殇珧秕瓠礤礅弪螵┅ㄩ痱弼轱躞礤礅弪瘐箬铄铄鳝鲠ㄣ潋痱弼轱躞礤礅弪螬瘐箬扉篝殇铄鳝鲠颟珧秕瓠礤礅弪螵┅铄鳝鲠颟痱镧ㄩ灬忮瘐箬扈疳轵躞邃栳钿屐灬忮祗┅ㄩ铘弪ㄦ矧磲铋⑻立殇┅┅┅换箝钽翳稚箴邈殒殂狒轱骘蜮殇翳狒戾瘿毂觳换麇轭趄镤蹉犷徜溟糸镱犰羼毂璨ㄤ彐躅泔铞弪舡栳钿戾麸栾戾鲠鲩灬忮祗戾舄è殇鲠颦殇鲠颟ㄨ镬ㄩ铘弪ㄦ矧磲铋⑷立殇┅灬猸殇ㄡ篌镢殇灬忮祗┅ㄩ灬猸殇躅戾篌礤礅弪殇栾戾灬忮飙羼螵瘐箬殇栾戾灬忮飙羼螵瘐箬磲脲瓠翦蝽吼蝈溟汜翦у横蜱扉篝ㄩ铘弪ㄦ矧磲铋⑻立殇┅栾戾┅鲩舡筱镳鲩舂┅栾戾┅ㄤ彐躅泸遽翦箴邈獒飙蝈灬糸镱鲠骣轭篝ㄩㄡ钿ㄥㄦ鲳衢颦驽狒躜骣皓鲩舡箫螋驽狒躜濯翦眇矧狎栳汶礤礅弪ㄦ鲳衢颦鲠祯骣皓鲰殓铒蝈洵箫螋扉篝┅铋ㄣ镱è羼ㄦ轵篝鲠飑舂扉篝ㄦ躅汜祆瓠簌礅镬ы犭瀛ㄦ轵篝蝈篝鲠飑┅洪铙翎钽轭篝横蜱扉篝ㄦ鲳衢颦鲠祯骣皓┅┅è羼ㄦ轵篝鲠飑э翳弪螬扉篝ㄦ躅汜祆瓠簌礅镬ы犭瀛ㄦ轵篝ㄦ轵篝蝈篝鲠飑┅洪铙翎钽轭篝横蜱蝈篝ㄦ轵篝蝈篝鲠飑┅┅è簌礅镬ㄦ轵篝蝈篝鲠飑┅扉篝ㄦ躅汜祆瓠簌礅镬ы犭瀛ㄦ轵篝蝈篝鲠飑┅洪铙翎钽轭篝横蜱扉篝ㄦ鲳衢颦鲠祯骣皓┅┅祜镳骘骘蝽轭蝈篝鲠飑泔祆邈ㄦ躅汜祆瓠簌礅镬ы犭瀛ㄦ轵篝骘蝽┅洪铙翎钽轭篝横蜱蝈篝骘蝽┅┅┅ㄤ彐躅珏舡趄犷箧矧磲糸镱翎忪瀛鲠祯ㄦ鲠鲠祯弩ㄤ镬轶鲠鲠祯弩戾è脲ㄦ轵篝鲠飑┅ㄣ镱è羼踽骣犰脲蝈趱蝾鲠飑è犷ㄥ豉疱脲矧ㄥ箦泔钿鲠飑骣犰翡飙痱邈邃弩箦泔钿鲠飑骣犰┅蝈趱蝾ㄣ镱骣犰ㄣ滗鲠飑┅è礤礅弪脲Ж雉桢蝮舂蝈趱蝾鲠飑铋飑┅┅换铒麇疳篌翳蝻貔玷翳屮趄狍犷躞轭溴驽狒躜瀛趄犷箧矧憝翎忪濯麸换骈钿秕麒弭桢翳弪轶箫礤翳轭麸滹ㄤ彐躅泔铞弪舡眚蟓鲠颦屮趄鲠蝮鲩轭篝珧秕痼灬忮祗祜镳骘鲠轭鲠蝮滹麒孱ㄡ钿鲠颦鲠颟铒礤礅弪轭篝鲩舡轭篝犷沐螵┅祜镳骘骣轭鲠颦屮趄鲠颟滹戾è麸滹ㄡ篌镢ㄦ鲳衢颦驽狒躜骣皓轭溴驽狒躜瀛趄犷箧矧憝翎忪濯┅麒孱麸滹戾舄è徙沐篌ㄦ轵篝蝈篝麸滹┅鲠祯弩蝈篝蝈篝麸滹┅鲠ㄧ弭趄犷箧矧磲糸镱翎忪瀛鲠祯ㄦ鲳衢颦鲠祯骣皓鲠祯弩┅铄黩屐ㄩㄡ钿ㄣ潋鲠飑铒ㄥㄣ狎鲠飑毫文┅ㄣ蝈狒瀛箴邈獒飙蝈灬糸镱鲠骣轭篝┅┅ㄩ铄黩屐ㄡ滗蝈祗麸鲩徙沐篌铄黩屐鲩舂┅┅瘐箬轭篝鲩舡轭篝犷沐螵┅┅换换提忮殳深篝箦戾泗轱躅箫祧邃狒痱弩孱躞邃镱禊骘т轵翎腴铉换换灬忮旎屮翦铙轱镦翎忪骘蝽狒鏖祆忮蝈聃轵邃换换翳蝻豸轭轶殇孱糸汜麸翳狒骘鲠颦屮趄ㄤ彐躅泔铞弪舡眚蟓蝈飙屮趄ㄦ遽趔鲩轭篝灬忮珧秕痼灬忮祗麒孱铒礤礅弪灬忮鲩舡轭篝犷沐螵┅祜镳骘骣轭驽狒滹戾è麸滹ㄡ篌镢ㄦ鲳衢颦驽狒躜骣皓蝈灬糸镱屮趄岘趄犷箧矧憝翎忪濯┅麒孱麸滹戾舄è徙沐篌ㄦ轵篝蝈篝麸滹┅鲠祯弩蝈篝蝈篝麸滹┅鲠ㄧ弭趄犷箧矧磲糸镱翎忪瀛鲠祯ㄦ鲳衢颦鲠祯骣皓鲠祯弩┅铄黩屐ㄩㄣ潋鲠飑ㄣ蝈狒瀛箴邈獒飙蝈灬糸镱鲠骣灬忮飑┅ㄩ铄黩屐ㄡ滗蝈祗麸鲩徙沐篌铄黩屐鲩舂┅┅瘐箬灬忮鲩舡轭篝犷沐螵┅ㄤ彐躅泔铞弪舡蝈灬糸镱豉疱轭骘麸鲩蝈鲩轭篝灬忮飑戾è痱邃蝈飙箫螋蝈飑┅祜镳骘豉疱轭蝈灬糸镱豉疱汨邈氇滹麒孱矧ㄥㄦ轵篝豉疱痱邃翡飙痱邈邃弩ㄦ轵篝豉疱痱邃┅戾舄è徙沐篌ㄣ徜豉疱┅鲠祯弩ㄣ滗豉疱┅铄黩屐ㄩ鲠祯弩祜镳骘骘蝽轭鲠祯弩泔祆邈ㄦ躅汜祆瓠簌礅镬ы犭瀛ㄦ轵篝骘蝽┅洪铙翎钽灬忮横蜱蝈篝骘蝽┅┅┅ㄩ铄黩屐ㄡ滗蝈祗麸鲩徙沐篌铄黩屐鲩舂┅蝈趱蝾鲩舂┅┅换麇汜屮翦钿翳轶麸犰祜骘铄鲩袅脑徙沐篌矧ㄤ彐躅徜洵蝈祗麸鲩箪雉蝈祗鲩舂ㄩ蝈祗ㄣ狍箪雉鲩舡箫螋箦翩鲩舡箫螋鲩舂ㄡ痧孱鲩舡箫螋鲩舂蝈祗┅鲩舡簌铘狲箦翩鲩舡簌铘狲鲩舂ㄡ痧孱鲩舡簌铘狲鲩舂蝈祗┅鲩舡翦铙遽钿狍疱泗箦翩鲩舡翦铙遽钿狍疱泗鲩舂ㄡ痧孱鲩舡翦铙遽钿狍疱泗鲩舂蝈祗┅鲩舡溟筱秕蝮箦翩鲩舡溟筱秕蝮鲩舂ㄡ痧孱鲩舡溟筱秕蝮鲩舂蝈祗┅鲩舡箦磲铘殂箦翩鲩舡箦磲铘殂鲩舂ㄡ痧孱鲩舡箦磲铘殂鲩舂蝈祗┅鲩舡痱矬镤箦翩鲩舡痱矬镤鲩舂ㄡ痧孱鲩舡痱矬镤鲩舂蝈祗┅铋飑┅ㄤ彐躅骈钿珧秕瓠镦灬忮灬忮珧秕痼ㄤ镬轶ㄧ蝻躔珧秕痼ㄩ礤礅弪灬忮蝈篝珧秕皓蝈趱蝾ㄦ轵篝珧秕皓┅┅换蔑铙趄蹉糸镱镦筱镳犷镦珧秕痖铉ㄤ彐躅磲脲鲩舡栳钿屐ㄩ灬忮祗ㄩㄡ篌镢殇灬忮祗ㄩ铘弪ㄦ矧磲铋⑻立殇┅ㄩ铘弪ㄦ矧磲铋⑷立殇┅┅ㄤ彐躅泔铙趄蹉舡鲩舡筱镳ㄥ聃犰轸殄戾耋灬忮祗ㄡ痧孱ㄦ矧羼轭羼踽扉糸弩泔祆邈磲脲瓠翦蝽吼蝈溟汜翦у横蜱扉篝磲脲鲩舡栳钿屐ㄣ狎羼酴灬忮祗磲脲鲩舡栳钿屐ㄣ潋羼酴灬忮祗┅┅ㄦ矧戾轭戾耋泔祆邈磲脲瓠翦蝽吼蝈溟汜翦ъ羼横蜱扉篝磲脲鲩舡栳钿屐ㄣ狎戾瘵灬忮祗磲脲鲩舡栳钿屐ㄣ潋戾瘵灬忮祗┅┅┅ㄤ彐躅泔铙趄蹉舡鲩舡珧秕痼ㄧ蝻躔犰轶舂ㄦ矧珧篝轭珧秕瓠犰轶狃疱钿戾è珧秕ㄩ铘弪ㄦ矧磲铋⑻立ㄣ狎珧篝颟┅ㄧ礤礅弪ㄣ潋珧篝颟┅ㄦ矧屐轭珥屙忮蝮泔祆邈磲脲瓠翦蝽吼蝈溟汜翦ч钸横蜱扉篝屐珧秕皓┅┅ㄤ彐躅珏舡珧秕瓠镦灬忮灬忮珧秕瓠犰轶舂ㄤ镬轶ㄧ蝻躔珧秕瓠犰轶灬忮飑ㄩ礤礅弪灬忮蝈篝珧秕皓蝈趱蝾ㄩ铘弪ㄦ矧磲铋⑻立ㄦ轵篝珧秕皓┅┅┅换蔑溴骘骈钿轭戾耋犷羼踽扉糸弩骝镯筱镳邃篝蝓泗躜弩ㄤ彐疳蜥礤翦戾耋铋泔铞孱殄铘麸篝矧戾耋轭珈镡犰ㄤ彐躅黠螂秕舡筱镳轭绛蝈篝蜷泗轱铙眚蟓痼镝忾钿轭绛箦舂麒孱忾钿轭绛箦戾舄è羼踽扉糸弩铋飑麸瓠栳钿屐ㄧ弭鲠颦铛痼镝栳钿屐眚蟓痼镝┅ㄡ祆蝈祗痼镝扉篾眚蟓痼镝┅蝈祗祜镳骘蝈轭犰飙蝈祗狃疱钿轭躅戾篌ㄡ篌镢蝈飙箫螋蝈飑鲰箴邈獒飙灬忮飙栳汶扉篝扉篝蝈飑┅┅换篷踽扉糸弩狎牾篝汜煦蹯狒邃骝镯翳忾钿轭珞换轭轸獒祆滹瞌怙翳弪麸溟篝轭珲轶忮赭邋羼踽扉糸弩翳狒换泔蝌弩痫钿麸羼踽扉糸弩轭稚犷翳矬翳狒泔蝌弩痫钿麸珧秕痖铉ㄦ矧忾钿轭轭忾钿轭绛箦滹戾è鲠虮ㄣ狎忾钿轭绌┅ㄦ矧鲠虿轭ㄣ潋忾钿轭绌滹躅戾篌ㄥ耢鲠虮鲠虿瘐箬铄ㄣ镱鲠虮鲠虿羼踽扉糸弩呼弩＇灬礅溽铄镬洎矧ㄥ聃犰铄镬洎ㄡ钿ㄥ耢ㄣ狎铄鳗ㄣ潋镬洎ㄥ耢ㄣ潋铄鳗ㄣ狎镬洎┅┅┅┅换泔祆邈糸铉翳戾耋轶盹蝈泔眇戾轸滹铄换怡麽祀轭滹黝翳趄邋骘翳泔铑邈翦鸵箦翩戾耋铋飑ㄣ镬戾泗戾耋骝镯蝈祗麸瓠栳钿屐蝈祗忾钿轭绛箦铋飑鲠祯弩羼踽扉糸弩戾耋┅┅ㄤ彐躅泔祆邈舡戾耋骝镯蝈祗麸瓠栳钿屐蝈飙扉篝忾钿轭珞栾戾蟓箫驷颟换情鲥沲蝌孱麸瓠栳钿屐骈钿犰翳蝈灬糸镱换麒殂狎灬忮祆邃鏖翳翳轶栳钿屐矧鏖翳灬忮怙躅麸翳轶栳钿屐换龄戾耋忮赭邋遽汨镦翳弩灬忮祗犷遽汨镦翳栾戾麇鲥泔礤徙蝻篌换箫驷虍澡孱骘遽汨镦翳弩蝈祗骈钿犰翳彘栳钿屐狎珲礤铘换徜翳弩麸栾戾蟓箫驷犷蝈沲蝮戾è麸瓠蝈祗ㄦ矧蝈轭蝈飙扉篝骈祠弪戾è蝈扈ㄧ弭鲠颦铛蝈飙栳钿屐蝈飑┅ㄩㄩ蟓祜汜祆羼蹰鲠戾铘蝈扈麸瓠栳钿屐忾钿轭珞痱镧ㄦ矧栾戾轭栾戾蟓箫驷滹躅戾篌ㄥ耢蝈扈栾戾瘐箬铄ㄣ镱蝈扈栾戾戾耋呼弩＇羼踽飑┅蝈飑┅┅ㄦ矧蝈轭麸瓠蝈祗滹ㄦ矧驽狒鲠轭蝈飙骒轶蝈飑滹戾è鲠ㄦ鲳衢颦鲠祯驽狒鲠飑┅ㄩ扉篝鲠颟ㄦ矧鲠轭鲠滹ㄩㄩ蟓栳钿屐鲠鲠飑ㄣ镬戾泗戾耋骝镯蝈祗ㄧ弭鲠颦铛鲠飑蝈飙扉篝忾钿轭珞ㄣ镱ㄧ弭鲠颦铛鲠飑栾戾蟓箫驷颟┅ㄩㄩ蟓栳钿屐鲠鲠颟ㄣ镬戾泗戾耋骝镯蝈祗ㄧ弭鲠颦铛鲠颟蝈飙扉篝忾钿轭珞ㄣ镱ㄧ弭鲠颦铛鲠颟栾戾蟓箫驷颟┅┅┅┅ㄤ彐躅轶祜汜祆羼蹰鲠戾铘ㄨ璨忾钿轭珞礤礅弪璨ㄧ弭忾钿轭珞骘颦栳钿屐璞忾钿轭珞┅换换铄骢钽糸镱骘泔铞弪糸铉篝蜷铉轭痼镝麸簌礅镬ㄤ彐躅眚蟓躅篝蜷铉痼镝痼镝换铒瞽溴篝蝓泗轹戾è铄鳝痼镝ㄣ镳痼镝痼镝┅箦翩痼镝礤篌徵铄鳝痼镝ㄩ蝈飙痼镝礤篌徵痼镝┅眚蟓躅篝蜷铉蝈痼镝礤篌徵痼镝┅┅箦翩痼镝扉篾铄鳝痼镝ㄦ矧蝈轭痼镝扉篾痼镝泔祆邈眚蟓躅篝蜷铉蝈蝈飑┅箦翩痼镝麋扉篾铄鳝痼镝ㄦ矧蝈轭痼镝麋扉篾痼镝泔祆邈眚蟓躅篝蜷铉蝈蝈飑┅铄鳝痼镝┅ㄤ彐躅眚蟓躅篝蜷铉蝈蝈飑戾è铄鳝蝈ㄣ镳蝈蝈飑┅箦翩蝈飙屮趄铄鳝蝈飑ㄦ矧骣轭蝈飙屮趄蝈飑泔祆邈眚蟓躅篝蜷铉骣骣皓┅箦翩蝈飙豉疱铄鳝蝈飑眚蟓躅篝蜷铉鲠祯蝈飙豉疱蝈飑┅箦翩蝈飙箫螋铄鳝蝈飑眚蟓躅篝蜷铉鲠祯蝈飙箫螋铄鳝蝈飑┅箦翩蝈飙骒轶铄鳝蝈飑ㄦ矧骣轭蝈飙骒轶蝈飑泔祆邈眚蟓躅篝蜷铉骣骣皓┅铄鳝蝈飑ㄤ彐躅眚蟓躅篝蜷铉骣ㄦ鲳戾舄è沲蝌孱舡骣瓠驽狒躜ㄦ鲳衢颦驽狒躜骣皓鲩趄殒殄洵驽狒躜灬篝疳翳驽狒躜沲蝌孱舡骣瓠驽狒躜濠┅换灬篝疳翳驽狒躜轶铒镳骘狒镯殂驽狒躜弩换怩蝈趱蝾翳灬篝驽狒躜骘疳翳磲脲骣疳轵烘遽趱蝈眚蟓躅篝蜷铉鲠祯ㄦ鲳衢颦驽狒躜骣皓忽犰蹂眚蟓躅篝蜷铉鲠祯ㄦ鲳衢颦鲠祯骣皓┅┅ㄤ彐躅眚蟓躅篝蜷铉鲠祯鲠飑ㄩ扉篝鲠飑ㄦ矧屐轭鲠泔祆邈ㄩ篝蜷铉屐蝈徜骝镯篝蜷铉屐屐┅ㄩ篝蜷铉鲠飑蝈徜骝镯篝蜷铉鲠飑鲠飑┅